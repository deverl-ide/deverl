%% =====================================================================
%% @author
%% @copyright
%% @title
%% @version
%% @doc This module contains library functions to display common dialogs.
%% @end
%% =====================================================================

-module(lib_dialog_wx).

-include_lib("wx/include/wx.hrl").

%% API
-export([
	get_dir/1,
	get_existing_dir/1,
	text_input_dialog/5,
	msg_error/2,
	msg_notice/2,
	save_changes_dialog/2]).
	
-define(ERROR_CAPTION, "The operation could not continue.").
-define(ID_DISCARD_CHANGES, 12345).

%% =====================================================================
%% Client API
%% =====================================================================

%% =====================================================================
%% @doc Get a directory from the user.

get_dir(Parent) ->
	dir_dialog(Parent, []).
	
	
%% =====================================================================
%% @doc Get an existing directory from the user.
	
get_existing_dir(Parent) ->
	dir_dialog(Parent, [{style, ?wxDD_DIR_MUST_EXIST}]).
	

%% =====================================================================
%% @doc Display a simple two button, single line text input to the user.
%% The events generated by the Ok button will propogate to the parent
%% of this dialog if a callback function is not supplied.
%% The text input is supplied as userData to the event handler.
%% If a callback was supplied, the callback will be invoked 
%% (in another process) to handle the event. The callback should be of 
%% arity 2. fun(EventRecord::wx(), EventObject::wxObject()).
%% The dialog is available in the obj field of EventRecord::wx().
%% Call skip on EventObject::wxObject() to allow default behaviour of
%% buttons.

text_input_dialog(Parent, Title, HelpStr, OkLabel, Options) ->
	AffirmitiveId = proplists:get_value(affirmitive_id, Options, ?wxID_OK),
	Callback = proplists:get_value(callback, Options),
	InitText = proplists:get_value(init_text, Options, []),
	
	Dialog = wxDialog:new(Parent, ?wxID_ANY, Title),

	Panel = wxPanel:new(Dialog),
	Sz = wxBoxSizer:new(?wxVERTICAL),
	wxSizer:addSpacer(Sz, 10),

	wxSizer:add(Sz, wxStaticText:new(Panel, ?wxID_ANY, HelpStr),
	  [{border,10}, {flag, ?wxEXPAND bor ?wxLEFT}]),
	wxSizer:addSpacer(Sz, 7),
	Input = wxTextCtrl:new(Panel, ?wxID_ANY, []),
	wxTextCtrl:setValue(Input, InitText),
	wxSizer:add(Sz, Input, [{border,10}, {flag, ?wxEXPAND bor ?wxLEFT bor ?wxRIGHT}, {proportion, 1}]),
	wxSizer:addSpacer(Sz, 15),

	ButtonSz = wxBoxSizer:new(?wxHORIZONTAL),
	wxSizer:addSpacer(ButtonSz, 10),
	wxSizer:add(ButtonSz, wxButton:new(Panel, ?wxID_CANCEL,
	  [{label,"Cancel"}]), [{border,10}, {flag, ?wxEXPAND bor ?wxBOTTOM}]),
	DefButton = wxButton:new(Panel, AffirmitiveId, [{label,OkLabel}]),
	wxButton:setDefault(DefButton),
	wxSizer:add(ButtonSz, DefButton, [{border,10}, {flag, ?wxEXPAND bor ?wxBOTTOM bor ?wxLEFT}]),
	wxSizer:addSpacer(ButtonSz, 10),
	wxSizer:add(Sz, ButtonSz),

	EvtOptions = [{userData, Input}],
	Result = case Callback of
		undefined -> 
			%% Force events to propagate beyond this dialog
			wxDialog:setExtraStyle(Dialog, wxDialog:getExtraStyle(Dialog) band (bnot ?wxWS_EX_BLOCK_EVENTS)),
			[];
		Cb -> [{callback, Cb}]
	end,
	wxButton:connect(Dialog, command_button_clicked, EvtOptions ++ Result),

	wxPanel:setSizer(Panel, Sz),
	wxSizer:layout(Sz),
	wxSizer:setSizeHints(Sz, Dialog),
	wxDialog:show(Dialog),
	wxWindow:setFocusFromKbd(Input).
	

%% =====================================================================
%% @doc Display an error message to the user.

msg_error(Parent, Msg) ->
	msg_dialog(Parent, Msg, ?ERROR_CAPTION, ?wxICON_ERROR bor ?wxOK).


%% =====================================================================
%% @doc Display a notice to the user.
	
msg_notice(Parent, Msg) ->
	msg_dialog(Parent, Msg, "Notice..", ?wxICON_INFORMATION bor ?wxOK).	


%% =====================================================================
%% @doc Create a "Save changes" dialog.

save_changes_dialog(Parent, Filename) ->
	Dialog = wxDialog:new(Parent, ?wxID_ANY, []),
	Sz = wxBoxSizer:new(?wxHORIZONTAL),
	wxDialog:setSizer(Dialog, Sz),
	% wxSizer:addSpacer(Sz, 10),
	wxSizer:addSpacer(Sz, 30),
	% Sz2 = wxBoxSizer:new(?wxHORIZONTAL),
	%% replace with icon
	Icon = wxPanel:new(Dialog, [{size, {70,70}}]),
	wxPanel:setBackgroundColour(Icon, ?wxWHITE),
	wxSizer:add(Sz, Icon, [{flag, ?wxTOP}, {border, 20}]),
	wxSizer:addSpacer(Sz, 20),

	
	Sz2 = wxBoxSizer:new(?wxVERTICAL),
	St = wxStaticText:new(Dialog, ?wxID_ANY, "The document \"" ++ Filename ++ "\" has been modified. Would you like to save the changes?", []),
	wxStaticText:wrap(St, 320),
	Font = wxDialog:getFont(Dialog),
	wxFont:setWeight(Font, ?wxFONTWEIGHT_BOLD),
	wxStaticText:setFont(St, Font),
	St2 = wxStaticText:new(Dialog, ?wxID_ANY, "All changes will be lost if you don't save."),
	wxStaticText:wrap(St2, 320),
	wxFont:setWeight(Font, ?wxFONTWEIGHT_NORMAL),
	wxFont:setPointSize(Font, wxFont:getPointSize(Font) - 2),
	wxStaticText:setFont(St2, Font),
	wxSizer:add(Sz2, St, []),
	wxSizer:addSpacer(Sz2, 10),
	wxSizer:add(Sz2, St2, []),
	wxSizer:addSpacer(Sz2, 20),
	
	ButtonSz = wxBoxSizer:new(?wxHORIZONTAL),
	Handler = 
	fun(Id) -> 
		Fun = fun(_,_) -> 
			wxDialog:endModal(Dialog, Id)
		end,
		Fun
	end,
	ButtonSzFlags = wxSizerFlags:border(wxSizerFlags:new(), ?wxRIGHT, 15),
	Discard = wxButton:new(Dialog, ?wxID_REVERT_TO_SAVED, [{label, "Discard Changes"}]),
	wxSizer:add(ButtonSz, Discard, wxSizerFlags:align(ButtonSzFlags, ?wxALIGN_LEFT)),
	wxSizer:addStretchSpacer(ButtonSz),
	wxSizer:add(ButtonSz, wxButton:new(Dialog, ?wxID_CANCEL), ButtonSzFlags),
	Save = wxButton:new(Dialog, ?wxID_SAVE, []),
	wxButton:setDefault(Save),
	wxSizer:add(ButtonSz, Save, []),
	wxButton:connect(Discard, command_button_clicked, [{callback, Handler(?wxID_REVERT_TO_SAVED)}]),
	wxButton:connect(Save, command_button_clicked, [{callback, Handler(?wxID_SAVE)}]),
	wxSizer:add(Sz2, ButtonSz, [{flag, ?wxEXPAND}, {proportion, 1}]),
	wxSizer:addSpacer(Sz2, 20),
	
	wxSizer:add(Sz, Sz2, [{flag, ?wxTOP}, {border, 20}]),
	wxSizer:addSpacer(Sz, 20),
	
	wxSizer:fit(Sz, Dialog),
	wxSizer:layout(Sz),
	wxDialog:layout(Dialog),
	Dialog.

	
%% =====================================================================
%% Internal functions
%% =====================================================================	

%% =====================================================================
%% @doc Display a choose directory dialog; gets a directory grom the 
%% user.
%% @private

dir_dialog(Parent, Args) ->
	Dialog = wxDirDialog:new(Parent, Args),
	case wxDirDialog:showModal(Dialog) of
		?wxID_OK -> 
			wxDirDialog:getPath(Dialog);
		?wxID_CANCEL -> cancelled
	end.
	

%% =====================================================================
%% @doc Display a message to the user.

-spec msg_dialog(Parent, Msg, Caption, Styles) -> 'ok' when
	Parent :: wxWindow:wxWindow(),
	Msg :: string() | [],
	Caption :: string() | [],
	Styles :: integer(). %% For styles see wxMessageDialog

msg_dialog(Parent, Msg, Caption, Styles) ->
	Dialog = wxMessageDialog:new(Parent, Msg, [{style, Styles bor ?wxSTAY_ON_TOP}, {caption, Caption}]),
	wxMessageDialog:showModal(Dialog).